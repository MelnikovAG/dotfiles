typeset -A utils
utils=(
  direnv direnv
  eza eza
  fzf fzf
  ifne moreutils
  rg ripgrep
  shellcheck shellcheck
  tmux tmux
  watch watch
)
to_install=()
for binary pkg in ${(@kv)utils}; do
  if ! command -v $binary &> /dev/null; then
    to_install+=$pkg
  fi
done

# Install nerd-fonts
if [[ "$os" == 'Darwin' ]]; then
  if [[ ! -f "$HOME/Library/Fonts/HackNerdFont-Regular.ttf" ]]; then
    to_install+=font-hack-nerd-font
  fi
fi

if [[ "${#to_install[@]}" -eq 0 ]]; then
  return
fi

echo "The following utilities are not yet installed: $to_install"
read -t 2 -qs "response?Install them?"
case "$response" in
  y)
    echo "\nInstalling."
    if [[ "$os" == 'Darwin' ]]; then
      brew install $to_install
    else
      sudo apt-get install $to_install
    fi
    ;;
  *)
    echo "\nSkipping install."
    ;;
esac
